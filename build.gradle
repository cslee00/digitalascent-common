/*
 * Copyright 2017-2017 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id "java-library"
    id "groovy"

    id "com.digitalascent.gradle.project" version "0.3.5"
}

scmVersion {
    localOnly = true
    tag {
        prefix = 'release'
        versionSeparator = '/'
    }
}


group = "com.digitalascent"
sourceCompatibility = 1.8
targetCompatibility = 1.8
version = scmVersion.version

repositories {
    jcenter()
}

ext.versions = [
        "guava" : "24.0-jre",
        "slf4j" : "1.7.25",
]

dependencies {
    implementation group: 'com.google.guava', name: 'guava', version: "${versions.guava}"
    implementation group: 'org.slf4j', name: 'slf4j-api', version: "${versions.slf4j}"

    testRuntimeOnly group: 'org.slf4j', name: 'slf4j-simple', version: "${versions.slf4j}"
    testImplementation "org.codehaus.groovy:groovy-all:2.4.12"
    testImplementation "org.spockframework:spock-core:1.1-groovy-2.4"
    testRuntimeOnly('com.athaydes:spock-reports:1.3.1') {
        transitive = false // this avoids affecting version of Groovy/Spock
    }
}

javadoc {
    description = "Generates project-level javadoc for use in -javadoc jar"
    configure(options) {
        memberLevel = JavadocMemberLevel.PROTECTED
        author = true
        header = project.name

        def javadocLinks = [
                "http://docs.oracle.com/javase/8/docs/api/",
                "http://docs.oracle.com/javaee/7/api/",
                "https://google.github.io/guava/releases/${versions.guava}/api/docs/"
        ] as String[]

        links(javadocLinks)
        addStringOption('Xdoclint:none', '-quiet')
    }

    // Suppress warnings due to cross-module @see and @link references.
    // Note that global 'api' task does display all warnings.
    logging.captureStandardError LogLevel.INFO
    logging.captureStandardOutput LogLevel.INFO  // suppress "## warnings" message
}

contacts {
    'chris@digitalascent.com' {
        moniker 'Chris Lee'
        github 'cslee00'
        role 'owner'
        role 'notify'
    }
}

bintray {
    pkg {
        websiteUrl = "https://github.com/cslee00/${project.name}"
        issueTrackerUrl = "https://github.com/cslee00/${project.name}/issues"
        vcsUrl = "https://github.com/cslee00/${project.name}.git"
    }
}

wrapper {
    gradleVersion = "4.6"
}